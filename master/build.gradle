plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'eclipse'
}

repositories {
    maven {
        url = 'https://www.xypron.de/repository' //for glpk-java
    }
}

dependencies {
    implementation 'org.gnu.glpk:glpk-java:1.7.0'
    implementation project(':sushi-lib')
    implementation project(':jbse')
}

shadowJar {
    classifier = 'shaded'
    exclude 'tools.jar'
    relocate 'javassist', 'sushi.javassist'
    relocate 'jbse', 'sushi.jbse'
    relocate 'org.jacoco', 'sushi.org.jacoco'
    relocate 'org.objectweb', 'sushi.org.objectweb'
    relocate 'org.kohsuke', 'sushi.org.kohsuke'
    relocate 'org.gnu', 'sushi.org.gnu'
}

build.dependsOn 'shadowJar'

eclipse {
    classpath { 
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            withXml { xml ->
                def node = xml.asNode()
                def container = node.find { it.@path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }
                container.appendNode('attributes').appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:'/usr/local/lib/jni'])
            }
        }
    }
}
